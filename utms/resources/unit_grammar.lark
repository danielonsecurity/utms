%ignore /[ \t\f]+/
%ignore /#[^\n]*/

units: unit+

unit: "unit" ESCAPED_STRING "{" field* "}" ";"
field: length | start | names | timezone

length: "length" ":" (expression | block) ";"
start: "start" ":" (expression | block) ";"
names: "names" ":" "[" ESCAPED_STRING ("," ESCAPED_STRING)+ "]" ";"
timezone: "timezone" ":" SIGNED_NUMBER ";"

block: "{" ((expression | assignment | variable | keyword) ";")+ "}"
assignment: variable "=" expression
expression: expression "+" term   -> add
          | expression "-" term   -> sub
          | term

term: term "*" factor   -> mul
    | term "/" factor   -> div
    | term "//" factor  -> divint
    | term "%" factor   -> rest
    | factor

factor: keyword | variable | SIGNED_NUMBER | NUMBER | "(" expression ")"

keyword: input_keyword | length_keyword | timezone_keyword
variable: /[a-zA-Z_]\w*/

input_keyword: "input"
length_keyword: "length"
timezone_keyword: "timezone"

%import common.ESCAPED_STRING
%import common.NUMBER
%import common.SIGNED_NUMBER
%import common.WS
%ignore WS
